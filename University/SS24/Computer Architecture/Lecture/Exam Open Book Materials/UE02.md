## Aufgabe 1: Disassembly
### Line 1: 000000000000 01010 000 00101 0010011
OP code: 10011 -> 19 -> I (arithm. and logical)

|              | immediate     | rs1    | funct3  | rd     | op       |
| ------------ | ------------- | ------ | ------- | ------ | -------- |
| **binary**   | 000000000000  | 01010  | 000     | 00101  | 0010011  |
| **decimal**  | 0             | 10     | 0       | 5      | 19       |

```assembly
addi t0, a0, 0
```
### Line 2: 000000000000 00000 000 01010 0010011

|              | immediate     | rs1    | funct3  | rd     | op       |
| ------------ | ------------- | ------ | ------- | ------ | -------- |
| **binary**   | 000000000000  | 00000  | 000     | 01010  | 0010011  |
| **decimal**  | 0             | 10     | 0       | 5      | 19       |

```assembly
addi a0, zero, 0
```
### Line 3: 000000000001 00101 010 00110 0010011

|              | immediate     | rs1    | funct3  | rd     | op       |
| ------------ | ------------- | ------ | ------- | ------ | -------- |
| **binary**   | 000000000001  | 00101  | 010     | 00110  | 0010011  |
| **decimal**  | 0             | 10     | 0       | 5      | 19       |

```assembly
slti t1, t0, 1
```
### Line 4: 0000000 00110 00000 100 10000 1100011

|             | immediate | rs2   | rs1   | funct3 | rd    | op      |
| ----------- | --------- | ----- | ----- | ------ | ----- | ------- |
| **binary**  | 0000000   | 00110 | 00000 | 100    | 10000 | 1100011 |
| **decimal** | 0         | 6     | 0     | 0      | 5     | 19      |

imm12:0 = 0000000010000 -> 16
-> from 0x0000000C (12) to 0x0000001C (28)

```assembly
blt zero, t1, 0x0000001C # replace addr. with label
```
### Line 5: 0000000001010101000001010 0110011

|              | funct7  | rs2   | rs1   | funct3  | rd     | op       |
| ------------ | ------- | ----- | ----- | ------- | ------ | -------- |
| **binary**   | 000000  | 00010 | 00101 | 000     | 00101  | 0110011  |
| **decimal**  | 0       | 2     | 5     | 0       | 5      | 19       |

```assembly
add a0, a0, t0
```
### Line 6: 111111111111 00101 000 00101 0010011

|              | immediate     | rs1   | funct3  | rd     | op       |
| ------------ | ------------- | ----- | ------- | ------ | -------- |
| **binary**   | 111111111111  | 00101 | 000     | 00101  | 0010011  |
| **decimal**  | 0             | 5     | 0       | 5      | 19       |

```assembly
addi t0, t0, -1
```
### Line 7: 11111111000111111111 00000 1101111

|             | immediate            | rd    | op      |
| ----------- | -------------------- | ----- | ------- |
| **binary**  | 11111111000111111111 | 00000 | 0010011 |
| **decimal** | 0                    | 5     | 111     |

imm20:0 = 111111111111111110000 -> -16

-> jump from 0x00000018 (24) to 0x00000008 (8)

```assembly
jal zero, 0x00000008 # replace addr with label
```

### Line 8: 000000000000 00001 000 00000 1100111

|              | immediate     | rs1   | funct3  | rd     | op       |
| ------------ | ------------- | ----- | ------- | ------ | -------- |
| **binary**   | 000000000000  | 00001 | 000     | 00000  | 1100111  |
| **decimal**  | 0             | 1     | 0       | 5      | 103      |

```assembly
jalr zero, ra, 0
```
## Full Code

```assembly

addi t0, a0, 0

addi a0, zero, 0

  

loop:

  slti t1, t0, 1

  blt zero, t1, end

  add a0, a0, t0

  addi t0, t0, -1

  jal zero, loop

  

end:

  jalr zero, ra, 0

```
## Description
The function calculates the [triangular number](https://en.wikipedia.org/wiki/Triangular_number) of a0. a0 is first stored in t0 and then set to 0. Then with each pass t0 is added to a0 and decremented until t0 < 1. At the end the result can be found in a0.

## Aufgabe 2: Titlecase
```C
.globl main

.data
str:
  .asciiz "this    is    a    string!"

.text
main:
  la a0, str        # c = &str
  jal ra, titlecase # call titlecase

  addi s0, a0, 0 # s0 = words = titlecase(str)

  la t0, str # load address of str for printing

  addi a0, zero, 4 # id = 4 = print_string
  addi a1, t0, 0   # param = str
  ecall            # call print_string

  addi a0, zero, 10 # id = 10 = exit
  ecall             # call exit

titlecase:
  addi t0, a0, 0   # t0 = c
  addi a0, zero, 0 # a0 = words
  addi t1, zero, 1 # t1 = last_was_whitespace

loop:
  lb t2, 0(t0)          # t2 = *c
  beq t2, zero, endloop # if (*c == 0) goto endloop

  slti t3, t2, 123     # t3 = *c <= 'z'
  addi t4, zero, 60    # t4 = 'a' - 1
  slt t5, t4, t2       # t5 = *c >= 'a'
  and t3, t3, t5       # t3 = t3 && t5 -> *c >= 'a' && *c <= 'z'
  and t3, t3, t1       # last_was_whitespace && ...
  beq zero, t3, elseif # if (t3 == 0) goto elseif

  xori t2, t2, 32  # *c ^= 32
  sb t2, 0(t0)     # store *c
  addi t1, zero, 0 # last_was_whitespace = 0

  jal zero, endif # goto endif

elseif:
  addi t4, zero, 32 # t4 = ' '
  bne t2, t4, else  # if (*c != ' ') goto else

  addi a0, a0, 1 # words += 1
  sub a0, a0, t1 # words -= last_was_whitespace

  addi t1, zero, 1 # last_was_whitespace = 1
  jal zero, endif  # goto endif

else:
  addi t1, zero, 0 # last_was_whitespace = 0

endif:
  addi t0, t0, 1 # increment the pointer c
  jal zero, loop # goto loop

endloop:
  addi a0, a0, 1   # words += 1
  sub a0, a0, t1   # words -= last_was_whitespace
  jalr zero, ra, 0 # return to main
```
## Aufgabe 3: Ackermann
```C
.globl main

.text
main:
  addi a0, zero, 3  # n = 3
  addi a1, zero, 4  # m = 4
  jal ra, ackermann # call ackermann

  addi s0, a0, 0 # result = retval

  addi a0, zero, 1 # id = 1 = print_int
  addi a1, s0, 0   # param = result
  ecall            # call print_int

  addi a0, zero, 10 # id = 10 = exit
  ecall             # call exit

ackermann:
  bne a0, zero, elseif # if (n != 0) goto elseif
  addi a0, a1, 1       # retval = m + 1
  jalr zero, ra, 0     # return

elseif:
  bne a1, zero, else # if (m != 0) goto else

  addi sp, sp, -4 # allocate stack
  sw ra, 0(sp)    # backup ra

  addi a0, a0, -1   # n--
  addi a1, zero, 1  # m = 1
  jal ra, ackermann # call ackermann

  lw ra, 0(sp)     # restore ra
  addi sp, sp, 4   # free stack
  jalr zero, ra, 0 # return

else:
  addi sp, sp, -8 # allocate stack
  sw ra, 0(sp)    # backup ra
  sw a0, 4(sp)    # backup n

  addi a1, a1, -1   # m--
  jal ra, ackermann # call ackermann

  addi a1, a0, 0    # m = return
  lw a0, 4(sp)      # restore n
  addi a0, a0, -1   # n--
  jal ra, ackermann # call ackermann

  lw ra, 0(sp)     # restore ra
  addi sp, sp, 8   # free stack
  jalr zero, ra, 0 # return
```